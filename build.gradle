/*
 * Copyright 2018 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
  id = 'cd.go.contrib.elasticagent.kubernetes'
  pluginVersion = '3.5.0'
  goCdVersion = '19.3.0'
  name = 'Kubernetes Elastic Agent Plugin'
  description = 'Kubernetes Based Elastic Agent Plugins for GoCD'
  vendorName = 'ThoughtWorks, Inc.'
  vendorUrl = 'https://github.com/gocd/kubernetes-elastic-agents'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'kubernetes-elastic-agents'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.jar]
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'cd.go.contrib'
version = gocdPlugin.fullVersion(project)

repositories {
    jcenter()
    mavenLocal()
}

sourceSets {
    test {
        java {
            compileClasspath += configurations.compileOnly
            runtimeClasspath += configurations.compileOnly
        }
    }
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '19.12.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'joda-time', name: 'joda-time', version: '2.10.5'
    compile group: 'io.fabric8', name: 'kubernetes-client', version: '4.6.0'
    compile group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.6'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.29'

    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.12.2'
}

jar {
    from(configurations.compile) {
        into "lib/"
    }
//    from(sourceSets.main.java) {
//        into "/"
//    }
}
