/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (Instant.now().epochSecond / 60)}"

gocdPlugin {
  id = 'cd.go.contrib.elasticagent.kubernetes'
  pluginVersion = '5.0.0'
  goCdVersion = '22.1.0'
  name = 'Kubernetes Elastic Agent Plugin'
  description = 'Kubernetes Based Elastic Agent Plugins for GoCD'
  vendorName = 'Thoughtworks, Inc.'
  vendorUrl = 'https://github.com/gocd/kubernetes-elastic-agents'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'kubernetes-elastic-agents'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.jar]
}

group = 'cd.go.contrib'
version = gocdPlugin.fullVersion(project)

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

ext {
    deps = [
        gocdPluginApi: 'cd.go.plugin:go-plugin-api:25.3.0',
        kubernetesClientApi: 'io.fabric8:kubernetes-client-api:7.4.0',
    ]

    versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
    compileOnly project.deps.gocdPluginApi
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.2'
    implementation project.deps.kubernetesClientApi
    runtimeOnly group: 'io.fabric8', name: 'kubernetes-client', version: project.versions.kubernetesClientApi
    implementation group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.14'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.34'

    testImplementation project.deps.gocdPluginApi
    testImplementation platform('org.junit:junit-bom:6.0.0')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.6'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.20.0'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.3'
    testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.21.2'
    testImplementation group: 'uk.org.webcompere', name: 'system-stubs-jupiter', version: '2.1.8'
}

test {
    useJUnitPlatform()
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}
