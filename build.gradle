/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
  id = 'cd.go.contrib.elasticagent.kubernetes'
  pluginVersion = '3.8.2'
  goCdVersion = '20.9.0'
  name = 'Kubernetes Elastic Agent Plugin'
  description = 'Kubernetes Based Elastic Agent Plugins for GoCD'
  vendorName = 'Thoughtworks, Inc.'
  vendorUrl = 'https://github.com/gocd/kubernetes-elastic-agents'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'kubernetes-elastic-agents'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.jar]
}

group = 'cd.go.contrib'
version = gocdPlugin.fullVersion(project)

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '22.2.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1'
    implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation group: 'joda-time', name: 'joda-time', version: '2.11.1'
    implementation group: 'io.fabric8', name: 'kubernetes-client', version: '6.0.0'
    implementation group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.10'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.31'

    testImplementation group: 'cd.go.plugin', name: 'go-plugin-api', version: '22.2.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.7.0'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
    testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.15.3'
}

test {
    useJUnitPlatform()
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}
