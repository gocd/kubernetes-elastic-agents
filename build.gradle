/*
 * Copyright 2018 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'co.riiid.gradle' version '0.4.2'
}

apply plugin: 'java'
apply from: 'plugin-common.gradle'

project.ext.pluginVersion = '2.1.0'
project.ext.fullVersion = project.distVersion ? "${project.pluginVersion}-${project.distVersion}" : project.pluginVersion

group = 'cd.go.contrib'
version = project.fullVersion

project.ext.pluginDesc = [
        id         : 'cd.go.contrib.elasticagent.kubernetes',
        version    : project.version,
        goCdVersion: '18.10.0',
        name       : 'Kubernetes Elastic Agent Plugin',
        description: 'Kubernetes Based Elastic Agent Plugins for GoCD',
        vendorName : 'GoCD Contributors',
        vendorUrl  : 'https://github.com/gocd/kubernetes-elastic-agents'
]

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    mavenLocal()
}

sourceSets {
    test {
        java {
            compileClasspath += configurations.compileOnly
            runtimeClasspath += configurations.compileOnly
        }
    }
}

dependencies {
    compileOnly group: 'cd.go.plugin', name: 'go-plugin-api', version: '18.2.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.4'
    compile group: 'io.fabric8', name: 'kubernetes-client', version: '3.1.8'
    compile group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.5'
    compile group: 'org.freemarker', name: 'freemarker', version: '2.3.23'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.0.96-beta'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.3.0'
    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }

        filesMatching('version.properties') {
            expand project.pluginDesc
        }
    }
}

jar {
    from(configurations.compile) {
        into "lib/"
    }
//    from(sourceSets.main.java) {
//        into "/"
//    }
}

tasks.withType(Jar) { jarTask ->
    preserveFileTimestamps = false
    reproducibleFileOrder = true

    ['MD5', 'SHA1', 'SHA-256'].each { algo ->
        jarTask.outputs.files("${jarTask.archivePath}.${algo}")
        jarTask.doLast {
            ant.checksum file: jarTask.archivePath, format: 'MD5SUM', algorithm: algo
        }
    }


    manifest {
        attributes(
                'Go-Version': project.pluginDesc.goCdVersion,
                'Plugin-Revision': project.pluginDesc.version,
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}

task deploy(type: Copy) {
    delete '/Users/ganeshpatil/projects/gocd/server/plugins/external/*.jar.*'
    from jar
    into '/Users/ganeshpatil/projects/gocd/server/plugins/external'
}

githubRelease.dependsOn('assemble')

github {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'kubernetes-elastic-agents'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    tagName = "${project.version}${prerelease ? '-exp' : ''}"
    name = "${prerelease ? 'Experimental: ' : ''}${project.version}"
    targetCommitish = project.gitRevision
    body = """
      Refer to the [changelog](https://github.com/gocd/kubernetes-elastic-agents/blob/master/CHANGELOG.md#${
        project.pluginVersion.replace(".", "")
    }---${new Date().format('YYYY-MM-dd')}) for the changes made in this release
    """.stripIndent().trim()
    assets = jar.outputs.files.files
}
